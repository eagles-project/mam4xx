set(MO_PHOTO_VALIDATION_DIR ${MAM_X_VALIDATION_DIR}/mo_photo)
set(MO_PHOTO_VALIDATION_SCRIPTS_DIR ${MAM_X_VALIDATION_DIR}/scripts)


# These subdirectories contain Skywalker drivers for MAM4 parameterizations.
# Include directory for .mod files.

include_directories(${PROJECT_BINARY_DIR}/validation)

# We use a single driver for all mo_photo-related parameterizations.
add_executable(mo_photo_driver
               mo_photo_driver.cpp
               cloud_mod.cpp
               find_index.cpp
               calc_sum_wght.cpp
               interpolate_rsf.cpp
               jlong.cpp
               set_ub_col.cpp
               table_photo.cpp
               )

target_link_libraries(mo_photo_driver skywalker;validation;haero)

# Copy some Python scripts from mam_x_validation to our binary directory.
foreach(script
        compare_mam4xx_mam4.py)
  configure_file(
    ${MO_PHOTO_VALIDATION_SCRIPTS_DIR}/${script}
    ${CMAKE_CURRENT_BINARY_DIR}/${script}
    COPYONLY
  )
endforeach()
# stand_calc_sum_wght_ts_355
# Run the driver in several configurations to produce datasets.
set(TEST_LIST
    stand_cloud_mod_ts_355
    find_index_ts_355
    cloud_mod_ts_353
    stand_synthetic_calc_sum_wght_ts_355
    stand_synthetic_interpolate_rsf_ts_355
    stand_synthetic_jlong_ts_355
    stand_synthetic_table_photo_ts_355
    )
# # matching the tests and errors, just for convenience

set(DEFAULT_TOL 1e-13)
set(ERROR_THRESHOLDS
    ${DEFAULT_TOL}
    0
    ${DEFAULT_TOL}
    6e-8
    6e-6
    8e-6
    2e-6
   )

set(TestLabel "mo_photo")

foreach(input tol IN ZIP_LISTS TEST_LIST ERROR_THRESHOLDS)
  # copy the baseline file into place.

  configure_file(
    ${MO_PHOTO_VALIDATION_DIR}/mam_${input}.py
    ${CMAKE_CURRENT_BINARY_DIR}/mam_${input}.py
    COPYONLY
  )

  # add a test to run the skywalker driver
  add_test(run_${input} mo_photo_driver ${MO_PHOTO_VALIDATION_DIR}/${input}.yaml)
  set_tests_properties(run_${input} PROPERTIES LABELS ${TestLabel})

  # add a test to validate mam4xx's results against the baseline.
  # Select a threshold error slightly bigger than the largest relative error for the threshold error.
  # compare_mam4xx_mam4.py <module1.py> <module2.py> <check_norms> <threshold error>
  add_test(validate_${input} python3 compare_mam4xx_mam4.py mam4xx_${input}.py mam_${input}.py True ${tol})
  set_property(TEST validate_${input} PROPERTY SKIP_REGULAR_EXPRESSION "regex_fail_rel_tol")
  set_tests_properties(validate_${input} PROPERTIES DEPENDS run_${input})
  set_tests_properties(validate_${input} PROPERTIES LABELS ${TestLabel})
endforeach()

# Test set_ub_col separately, since we're only computing O3 "densities" and not
# O2 densities.
set(input "set_ub_col_ts_355")
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/compare_set_ub_col_mam4xx_mam4.py
  ${CMAKE_CURRENT_BINARY_DIR}/compare_set_ub_col_mam4xx_mam4.py
  COPYONLY
)
configure_file(
  ${MO_PHOTO_VALIDATION_DIR}/mam_${input}.py
  ${CMAKE_CURRENT_BINARY_DIR}/mam_${input}.py
  COPYONLY
)

# add a test to run the skywalker driver
add_test(run_${input} mo_photo_driver ${MO_PHOTO_VALIDATION_DIR}/${input}.yaml)
set_tests_properties(run_${input} PROPERTIES LABELS ${TestLabel})

# add a test to validate mam4xx's results against the baseline.
# Select a threshold error slightly bigger than the largest relative error for the threshold error.
# compare_mam4xx_mam4.py <module1.py> <module2.py> <check_norms> <threshold error>
add_test(validate_${input} python3 compare_set_ub_col_mam4xx_mam4.py mam4xx_${input}.py mam_${input}.py True ${tol})
set_property(TEST validate_${input} PROPERTY SKIP_REGULAR_EXPRESSION "regex_fail_rel_tol")
set_tests_properties(validate_${input} PROPERTIES DEPENDS run_${input})
set_tests_properties(validate_${input} PROPERTIES LABELS ${TestLabel})
